apply plugin: 'application'

dependencies {
    implementation project(':eureka-client-jersey3')
    implementation files('conf')
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'

    runtimeOnly 'org.slf4j:slf4j-simple:1.7.7'
}

startScripts.enabled = false
run.enabled = false

task runExampleService (dependsOn: [classes], type: JavaExec) {
    group = "Run tasks"
    description = "Run the example service"

    main = "com.netflix.eureka.ExampleEurekaService"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(["-Deureka.client.props=sample-eureka-service"])
}

task runExampleGovernatedService (dependsOn: [classes], type: JavaExec) {
    group = "Run tasks"
    description = "Run the example service"

    main = "com.netflix.eureka.ExampleEurekaGovernatedService"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(["-Deureka.client.props=sample-eureka-service"])
}

task runExampleClient (dependsOn: [classes], type: JavaExec) {
    group = "Run tasks"
    description = "Run the example client"

    main = "com.netflix.eureka.ExampleEurekaClient"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(["-Deureka.client.props=sample-eureka-client"])
}


task ExampleServiceStartScript(type: CreateStartScripts) {
    mainClassName = "com.netflix.eureka.ExampleEurekaService"
    applicationName = "ExampleEurekaService"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
    defaultJvmOpts = ["-Deureka.client.props=sample-eureka-service"]
}

task ExampleGovernatedServiceStartScript(type: CreateStartScripts) {
    mainClassName = "com.netflix.eureka.ExampleEurekaGovernatedService"
    applicationName = "ExampleEurekaGovernatedService"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
    defaultJvmOpts = ["-Deureka.client.props=sample-eureka-service"]
}

task ExampleClientStartScript(type: CreateStartScripts) {
    mainClassName = "com.netflix.eureka.ExampleEurekaClient"
    applicationName = "ExampleEurekaClient"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
    defaultJvmOpts = ["-Deureka.client.props=sample-eureka-client"]
}

applicationDistribution.into("bin") {
    from(ExampleServiceStartScript)
    fileMode = 0755

    from(ExampleGovernatedServiceStartScript)
    fileMode = 0755

    from(ExampleClientStartScript)
    fileMode = 0755
}

applicationDistribution.into("lib/conf") {
    from("conf")
    fileMode = 0755
}

tasks.withType(Tar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Zip).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

